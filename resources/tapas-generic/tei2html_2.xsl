<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:html="http://www.w3.org/1999/xhtml"
  xmlns:xd="http://www.oxygenxml.com/ns/doc/xsl"
  exclude-result-prefixes="xd"
  version="1.0">
  
  <xsl:output method="html"/>

  <xd:doc scope="stylesheet">
    <xd:desc>
      <xd:p><xd:b>Created on:</xd:b> 2014-04-21 by Syd Bauman</xd:p>
      <xd:p>Reads in an XHTML file (ostensibly generated by tei2html_1.xslt),
      and writes out a subset thereof with a few tweaks:</xd:p>
      <xd:ul>
        <xd:li>Subset: we extract the <tt>&lt;div class="tapas-generic"></tt> only</xd:li>
        <xd:li>elements with only text node children have whitespace normalized on those text nodes
        (This is primarily for human readability, but also because I'm not sure what happens
        if you use CSS *:before and *:after selectors on nodes that have leading or trailing
        whitespace, respectively.)</xd:li>
      </xd:ul>
      <xd:p>Change log:</xd:p>
      <xd:ul>
        <xd:li>2015-09-10 by Syd: write out only the <tt>&lt;div class="tapas-generic"></tt>
        for easier inclusion in the new TAPAS world order, which is not using iFrames.</xd:li>
      </xd:ul>
    </xd:desc>
  </xd:doc>
  
  <xsl:template match="/">
    <xsl:apply-templates select="/html:html/html:body/html:div[@class='tapas-generic']"/>
  </xsl:template>
  
  <xsl:template match="@*|node()">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template match="*[
        not( child::* )
    and not( child::comment() )
    and not( child::processing-instruction() )
    ]">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:value-of select="normalize-space(.)"/>
    </xsl:copy>
  </xsl:template>

</xsl:stylesheet>